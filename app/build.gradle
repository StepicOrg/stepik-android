apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

int minSdk = hasProperty('devMinSdk') ? devMinSdk.toInteger() : 15

def globalConf = rootProject.ext
android {

    compileSdkVersion 25
    buildToolsVersion "25.0.1"

    signingConfigs {
        debug {
            storeFile file(globalConf.debugKeystore)
            storePassword globalConf.debugKeystorePass
            keyAlias globalConf.debugAlias
            keyPassword globalConf.debugAliasPass
        }

        release {
            storeFile file(globalConf.keystore)
            storePassword globalConf.keystorePass
            keyAlias globalConf.alias
            keyPassword globalConf.aliasPass
        }
    }

    defaultConfig {
        applicationId "org.stepic.droid"
        minSdkVersion minSdk
        targetSdkVersion 25
        versionCode 152
        versionName "1.33.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // Enabling multidex support.
        multiDexEnabled true
    }


    productFlavors {
        sb {
            applicationId "org.stepic.droid.sb"
        }
        classic {
            applicationId "org.stepic.droid"
        }
        releasedev {
            applicationId "org.stepic.droid.release"
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    splits {
        abi {
            enable !project.hasProperty('noSplitBuild')
            reset()
            include 'armeabi-v7a', 'mips', 'x86', 'x86_64', 'arm64-v8a'
            //select ABIs to build APKs for
            universalApk false //generate an additional APK that contains all the ABIs
        }
    }

    // map for the version code
    project.ext.versionCodes = ['armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        // assign different version code for each output
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(com.android.build.OutputFile.ABI), 0) + android.defaultConfig.versionCode * 10
        }
    }

    // delete unaligned files
    android.applicationVariants.all { variant ->
        variant.assemble.doLast {
            variant.outputs.each { output ->
                File unaligned = output.packageApplication.outputFile;
                File aligned = output.outputFile
                if (!unaligned.getName().equalsIgnoreCase(aligned.getName())) {
                    println "deleting " + unaligned.getName()
                    unaligned.delete()
                }
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        abortOnError false
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        debug {
            signingConfig signingConfigs.debug
            versionNameSuffix "DEV"
        }
    }
}

kapt {
    generateStubs = true
}

dependencies {

    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

    compile fileTree(dir: 'libs', include: ['*.jar'])

    //<DI>
    compile 'com.google.dagger:dagger:2.10'
    kapt 'com.google.dagger:dagger-compiler:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'
    //needed to resolve compilation errors, thanks to tutplus.org for finding the dependency

    compile 'com.jakewharton:butterknife:8.5.1'
    kapt 'com.jakewharton:butterknife-compiler:8.5.1'
    //</DI>

    compile 'com.android.support:multidex:1.0.1'
    compile 'com.google.code.gson:gson:2.7'

    compile 'de.hdodenhof:circleimageview:1.3.0'

    compile 'com.android.support:appcompat-v7:25.0.1'
    compile 'com.android.support:design:25.0.1'
    compile 'com.android.support:recyclerview-v7:25.0.1'
    compile 'com.android.support:cardview-v7:25.0.1'
    compile 'com.amulyakhare:com.amulyakhare.textdrawable:1.0.1'

    compile 'joda-time:joda-time:2.8'

    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    compile 'com.squareup:otto:1.3.8'

    // AppMetrica from maven central. Protobuf will be included as dependency.
    compile 'com.yandex.android:mobmetricalib:2.62'

    compile 'jp.wasabeef:recyclerview-animators:2.1.0'

    compile 'uk.co.chrisjenx:calligraphy:2.1.0'

    compile 'me.zhanghai.android.materialprogressbar:library:1.1.6'


    compile 'de.mrmaffen:vlc-android-sdk:1.9.8'

    compile 'org.jsoup:jsoup:1.8.3'

    compile 'com.google.firebase:firebase-messaging:10.2.0'
    compile "com.google.firebase:firebase-appindexing:10.2.0"
    compile 'com.google.android.gms:play-services-auth:10.2.0'
    compile 'com.google.firebase:firebase-core:10.2.0'
    compile 'com.google.firebase:firebase-crash:10.2.0'
    compile 'com.google.firebase:firebase-config:10.2.0'

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.facebook.android:facebook-android-sdk:4.17.0'
    compile 'com.vk:androidsdk:1.6.5'
    compile 'com.jakewharton.timber:timber:4.3.1'

    compile 'com.github.javiersantos:MaterialStyledDialogs:2.0'
    compile 'biz.kasual:materialnumberpicker:1.2.1'

    compile 'com.github.chrisbanes:PhotoView:1.3.1'

    compile "com.mixpanel.android:mixpanel-android:4.9.3"

    compile('com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:0.10.2@aar') {
        transitive = true
    }

    compile project(':androidsvg')

    compile 'com.facebook.stetho:stetho:1.4.2'
    compile 'com.facebook.stetho:stetho-okhttp3:1.4.2'

    // Required -- JUnit 4 framework
    testCompile 'junit:junit:4.12'
    // Optional -- Mockito framework
    testCompile 'org.mockito:mockito-core:2.7.21'

    androidTestCompile 'org.hamcrest:hamcrest-library:1.3'

    androidTestCompile 'com.android.support:support-annotations:25.1.0'

    androidTestCompile('com.android.support.test:runner:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test:rules:0.5') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.2') {
        exclude group: 'com.android.support'
    }
    androidTestCompile('com.android.support.test.espresso:espresso-intents:2.2.2') {
        exclude group: 'com.android.support'
    }

}

repositories {
    jcenter()
    maven {
        url "https://jitpack.io"
    }
    mavenCentral()
}
apply plugin: 'com.google.gms.google-services'
