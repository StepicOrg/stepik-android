apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'
apply from: '../code_quality_tools/jacoco.gradle'
apply from: '../code_quality_tools/findbugs.gradle'
apply from: '../code_quality_tools/checkstyle.gradle'
apply from: '../code_quality_tools/pmd.gradle'
apply from: '../code_quality_tools/ktlint.gradle'

android {
    compileSdkVersion versions.compileSdk
    buildToolsVersion versions.buildTools

    defaultConfig {
        applicationId "org.stepic.droid"
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk
        versionCode versions.code
        versionName versions.name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }


    signingConfigs {
        debug {
            storeFile file(debugKeystore)
            storePassword debugKeystorePass
            keyAlias debugAlias
            keyPassword debugAliasPass
        }

        release {
            storeFile file(keystore)
            storePassword keystorePass
            keyAlias alias
            keyPassword aliasPass
        }

        oldRelease {
            storeFile file(keystore)
            storePassword keystorePass
            keyAlias oldAlias
            keyPassword oldAliasPass
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }

    lintOptions {
        warningsAsErrors false // after fixing existing errors set to true for fixing warnings
        abortOnError false // after fixing existing errors set to true for failing fast
        lintConfig rootProject.file('lint.xml')
        htmlOutput rootProject.file("reports/lint/lint.html")
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release

            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro'
        }
        debug {
            minifyEnabled true
            useProguard true
            signingConfig signingConfigs.debug
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules.pro', 'proguard-rules-dev.pro'
            versionNameSuffix "DEV"

            testCoverageEnabled = false
        }

        //the apk of this type is signed by old prod keys
        //it makes apk testable (sign in, smart lock, etc are worked).
        //now new sign configs are used for providing to Google Play
        //assemble with oldProd on release/* branches
        oldRelease {
            initWith release
            signingConfig signingConfigs.oldRelease
            matchingFallbacks = ['release']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kapt {
    generateStubs = true
}

dependencies {
    implementation project(':androidsvg')
    implementation project(':model')
    implementation libraries.kotlinStd

    //<DI>
    implementation libraries.dagger
    kapt libraries.daggerCompiler
    implementation libraries.butterKnife
    kapt libraries.butterKnifeCompiler
    //</DI>

    implementation libraries.rxAndroid
    implementation libraries.rxJava2
    implementation libraries.rxKotlin

    implementation libraries.supportAppCompat
    implementation libraries.supportDesign
    implementation libraries.supportRecyclerView
    implementation libraries.supportCardView
    implementation libraries.multidex
    implementation libraries.constraintLayout
    implementation libraries.exoPlayerCore
    implementation libraries.exoPlayerUI
    implementation libraries.exoPlayerMediaSession

    implementation libraries.archExtensions
    implementation libraries.archViewModel

    implementation libraries.gson
    implementation libraries.retrofit
    implementation libraries.retrofitGsonConverter
    implementation libraries.retrofitRxJavaAdapter
    implementation libraries.jsoup
    debugImplementation libraries.stetho
    debugImplementation libraries.stethoOkHttp
    oldReleaseImplementation libraries.stetho
    oldReleaseImplementation libraries.stethoOkHttp

    implementation libraries.glide
    implementation libraries.glideTransformations
    kapt libraries.glideAnnotations

    implementation libraries.amplitude
    implementation libraries.appMetrica
    implementation libraries.branch
    implementation libraries.installreferrer

    implementation libraries.calligraphy
    implementation libraries.lottie

    implementation libraries.firebaseMessaging
    implementation libraries.firebaseAppindexing
    implementation libraries.googleauth
    implementation libraries.firebaseCore
    implementation libraries.firebaseConfig
    implementation libraries.firebasePerf

    implementation libraries.checkout

    implementation libraries.facebookSdk
    implementation libraries.vkSdk

    implementation libraries.timber

    implementation libraries.materialDialog
    implementation libraries.materialDialogCore
    implementation libraries.numberPicker
    implementation (libraries.datePicker) {
        exclude group: 'com.android.support'
    }
    implementation libraries.photoView
    implementation libraries.advancedRecycler
    implementation libraries.recyclerAnimators
    implementation libraries.materialProgress
    implementation libraries.circleImageView
    implementation libraries.ahbottomnavigation
    implementation libraries.circleIndicator
    implementation libraries.confetti
    implementation libraries.MPAndroidChart

    implementation libraries.shortcutBadger
    implementation libraries.StoriesKit
    implementation libraries.AdapterDelegates
    implementation libraries.Adapters

    debugImplementation libraries.leakCanary
    releaseImplementation libraries.leakCanaryNoOp
    oldReleaseImplementation libraries.leakCanaryNoOp

    implementation(libraries.crashlytics) {
        transitive = true
    }

    testImplementation libraries.junit
    testImplementation libraries.robolectric
    testImplementation libraries.mockitoCore
    testImplementation libraries.mockitoKt
    testImplementation libraries.jodaTime
    testImplementation libraries.hamcrest

    androidTestImplementation(libraries.supportTestRunner) {
        exclude group: 'com.android.support'
    }
    androidTestImplementation libraries.supportTestRules
    androidTestImplementation(libraries.espressoCore) {
        exclude group: 'com.android.support'
    }
    androidTestImplementation(libraries.espressoContrib) {
        exclude group: 'com.android.support'
    }
    androidTestImplementation(libraries.espressoIntents) {
        exclude group: 'com.android.support'
    }
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.getkeepsafe.dexcount'
apply plugin: 'com.google.firebase.firebase-perf'