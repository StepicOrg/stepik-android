repositories {
    jcenter()
}

configurations {
    ktlint
}

dependencies {
    ktlint libraries.ktlint
    ktlint project(":ktlint_rules")
    // additional 3rd party ruleset(s) can be specified here
    // just add them to the classpath (ktlint 'groupId:artifactId:version') and
    // ktlint will pick them up
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args = ["--disabled_rules=no-multi-spaces,package-name,final-newline,import-ordering", "src/main/java/org/stepik/**/*.kt"]
}
check.dependsOn ktlint

task ktlintFormat(type: JavaExec, group: "verification") {
    description = "Format Kotlin code style."
    main = "com.pinterest.ktlint.Main"
    classpath = configurations.ktlint
    args = ["-F", "--disabled_rules=no-multi-spaces,package-name,final-newline,import-ordering", "src/main/java/org/stepik/**/*.kt"]
    // to generate report in checkstyle format prepend following args:
    // "--reporter=plain", "--reporter=checkstyle,output=${buildDir}/ktlint.xml"
    // see https://github.com/shyiko/ktlint#usage for more
}